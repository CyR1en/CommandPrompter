/*
 * MIT License
 *
 * Copyright (c) 2020 Ethan Bacurio
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id "io.github.goooler.shadow" version "8.1.7"
    id 'java'
    id 'maven-publish'
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

PluginManifest pluginManifest = [
        name       : 'CommandPrompter',
        version    : new Version(major: 2, minor: 10, patch: 1, fix: 0, classifier: 'SNAPSHOT'),
        author     : 'CyR1en',
        description: 'Making Commands More Interactive!',
        entry      : 'com.cyr1en.commandprompter.CommandPrompter'
]

group = 'com.cyr1en'
version = pluginManifest.version.toString()

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo.cyr1en.com/snapshots' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.glaremasters.me/repository/towny/'}
    maven { url 'https://repo.william278.net/releases' }
    flatDir { dirs 'libs' }
}

dependencies {
    implementation 'com.cyr1en:kiso-utils:1.8-SNAPSHOT'
    implementation 'com.cyr1en:kiso-mc:1.8-SNAPSHOT'
    implementation 'net.wesjd:anvilgui:1.10.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.0'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.17'
    implementation "net.kyori:adventure-text-minimessage:4.17.0"
    implementation "dev.jorel:commandapi-bukkit-shade:9.5.3"
    implementation 'de.rapha149.signgui:signgui:2.4.1'

    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly "net.kyori:adventure-text-serializer-legacy:4.17.0"
    compileOnly "net.kyori:adventure-text-serializer-plain:4.17.0"
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.3.0'
    compileOnly "org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT"
    compileOnly 'com.github.LeonMangler:SuperVanish:6.2.18-3'
    compileOnly 'de.hexaoxi:carbonchat-api:3.0.0-beta.26'
    compileOnly 'com.github.mbax:VanishNoPacket:3.22'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'net.william278.husktowns:husktowns-common:3.0.5'

    // Local
    implementation fileTree(dir: 'libs', include: '*.jar')
    compileOnly fileTree(dir: 'libs/compileonly', include: '*.jar')
}

configurations.implementation {
    exclude group: 'joda-time', module: 'joda-time'
    exclude group: 'org.atteo.classindex', module: 'classindex'
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    archiveBaseName.set("$project.name")
    archiveClassifier.set('shaded')
    archiveVersion.set(pluginManifest.version.getFullVersion())

    relocate 'com.github.stefvanschie.inventoryframework', 'com.cyr1en.inventoryframework'
    relocate 'net.wesjd.anvilgui', 'com.cyr1en.anvilgui'
    relocate 'de.rapha149.signgui', 'com.cyr1en.signgui'
    relocate 'net.kyori.adventure', 'com.cyr1en.adventure'
    relocate 'net.kyori.examination', 'com.cyr1en.examination'
    relocate 'dev.jorel.commandapi', 'com.cyr1en.commandapi'
    relocate 'org.fusesource.jansi', 'com.cyr1en.jansi'
    relocate 'org.bstats', 'com.cyr1en.bstats'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'projectName': pluginManifest.name,
                'projectVersion': version,
                'projectAuthor': pluginManifest.author,
                'projectDescription': pluginManifest.description,
                'projectEntry': pluginManifest.entry
        )
        include 'plugin.yml'
    }
}

artifacts {
    archives shadowJar
}

publishing {
    repositories {
        maven {
            name = "Kakuno"
            url = uri("https://repo.cyr1en.com/snapshots")
            credentials {
                username = System.getenv("KAKUNO_USER")
                password = System.getenv("KAKUNO_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

class PluginManifest {
    String name, author, description, entry
    Version version
}

class Version {

    String major, minor, patch, fix, classifier, full

    String getFullVersion() {
        full = "$major.$minor.$patch${(fix as Integer) > 0 ? ".$fix" : ''}"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
