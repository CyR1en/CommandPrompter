/*
 * MIT License
 *
 * Copyright (c) 2020 Ethan Bacurio
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'java'
    id 'maven-publish'
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

PluginManifest pluginManifest = [
        name       : 'CommandPrompter',
        version    : new Version(major: 2, minor: 6, patch: 1, fix: 0, classifier: 'SNAPSHOT'),
        author     : 'CyR1en',
        description: 'Perfect companion plugin for inventory UI menu.',
        entry      : 'com.cyr1en.commandprompter.CommandPrompter'
]

group = 'com.cyr1en'
version = pluginManifest.version.toString()

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo.cyr1en.com/snapshots' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    flatDir { dirs 'libs' }
}

dependencies {
    implementation 'com.cyr1en:kiso-utils:1.8-SNAPSHOT'
    implementation 'com.cyr1en:kiso-mc:1.8-SNAPSHOT'
    implementation 'net.wesjd:anvilgui:1.9.0-SNAPSHOT'
    implementation 'io.github.rapha149.signgui:signgui:2.2.1'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation "dev.jorel:commandapi-bukkit-shade:9.2.0"
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.0'

    // Exclude these
    implementation 'me.lucko:jar-relocator:1.7'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.11'

    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly "net.kyori:adventure-text-serializer-legacy:4.13.1"
    compileOnly "net.kyori:adventure-text-serializer-plain:4.13.1"
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"
    compileOnly 'com.github.LeonMangler:SuperVanish:6.2.17'
    compileOnly 'com.github.mbax:VanishNoPacket:3.22'
    compileOnly 'org.jetbrains:annotations:23.0.0'

    // Local
    implementation fileTree(dir: 'libs', include: '*.jar')
    compileOnly fileTree(dir: 'libs/compileonly', include: '*.jar')
}

configurations.implementation {
    exclude group: 'joda-time', module: 'joda-time'
    exclude group: 'org.atteo.classindex', module: 'classindex'
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    archiveBaseName.set("$project.name")
    archiveClassifier.set(pluginManifest.version.classifier + "-Bundled")
    archiveVersion.set(pluginManifest.version.getFullVersion())

    relocate 'com.github.stefvanschie.inventoryframework', 'com.cyr1en.inventoryframework'
    relocate 'net.wesjd.anvilgui', 'com.cyr1en.anvilgui'
    relocate 'io.github.rapha149.signgui', 'com.cyr1en.signgui'
    relocate 'dev.jorel.commandapi', 'com.cyr1en.commandapi'
    relocate 'org.fusesource.jansi', 'com.cyr1en.jansi'
    relocate 'org.bstats', 'com.cyr1en.bstats'

    manifest {
        attributes "Bundled": true
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.register("lightJar", ShadowJar) {
    archiveClassifier.set(pluginManifest.version.classifier)
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]


    dependencies {
        exclude(dependency('com.mojang:brigadier'))
        exclude(dependency('com.github.stefvanschie.inventoryframework:IF'))
        exclude(dependency('me.lucko:jar-relocator'))
        exclude(dependency('org.ow2.asm:asm'))
        exclude(dependency('org.ow2.asm:asm-commons'))
        exclude(dependency('org.ow2.asm:asm-tree'))
        exclude(dependency('org.ow2.asm:asm-analysis'))
        exclude(dependency('io.github.rapha149.signgui:signgui'))
        exclude(dependency('dev.jorel:commandapi-bukkit-shade'))
    }
    relocate 'com.github.stefvanschie.inventoryframework', 'com.cyr1en.inventoryframework'
    relocate 'net.wesjd.anvilgui', 'com.cyr1en.anvilgui'
    relocate 'io.github.rapha149.signgui', 'com.cyr1en.signgui'
    relocate 'org.fusesource.jansi', 'com.cyr1en.jansi'
    relocate 'dev.jorel.commandapi', 'com.cyr1en.commandapi'
    relocate 'org.bstats', 'com.cyr1en.bstats'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'projectName': pluginManifest.name,
                'projectVersion': version,
                'projectAuthor': pluginManifest.author,
                'projectDescription': pluginManifest.description,
                'projectEntry': pluginManifest.entry
        )
        include 'plugin.yml'
    }
}

artifacts {
    archives shadowJar
    archives lightJar
}

publishing {
    repositories {
        maven {
            name = "Kakuno"
            url = uri("https://repo.cyr1en.com/snapshots")
            credentials {
                username = System.getenv("KAKUNO_USER")
                password = System.getenv("KAKUNO_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

class PluginManifest {
    String name, author, description, entry
    Version version
}

class Version {

    String major, minor, patch, fix, classifier, full

    String getFullVersion() {
        full = "$major.$minor.$patch${(fix as Integer) > 0 ? ".$fix" : ''}"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
