/*
 * MIT License
 *
 * Copyright (c) 2020 Ethan Bacurio
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id "io.github.goooler.shadow" version "8.1.7"
    id 'java'
    id 'maven-publish'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.18'
}


compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

PluginManifest pluginManifest = [
        name       : 'CommandPrompter',
        version    : new Version(major: 3, minor: 0, patch: 0, fix: 0, classifier: 'SNAPSHOT'),
        author     : 'CyR1en',
        description: 'Making Commands More Interactive!',
        entry      : 'com.cyr1en.commandprompter.CommandPrompter'
]

group = 'com.cyr1en'
version = pluginManifest.version.toString()

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo.cyr1en.com/snapshots' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.glaremasters.me/repository/towny/' }
    maven { url 'https://repo.william278.net/releases' }
    maven { url 'https://repo.opencollab.dev/main/' }
    flatDir { dirs 'libs' }
}

dependencies {
    paperweight.paperDevBundle("1.21.7-R0.1-SNAPSHOT")

    implementation 'com.cyr1en:kiso-utils:1.8-SNAPSHOT'
    implementation 'com.cyr1en:kiso-mc:1.8-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    implementation "dev.jorel:commandapi-bukkit-shade:10.1.2"

    // UI libs
    implementation 'net.wesjd:anvilgui:1.10.8-SNAPSHOT'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.11.2'
    implementation 'de.rapha149.signgui:signgui:2.5.4'

    compileOnly 'org.openjdk.nashorn:nashorn-core:15.4'
    compileOnly 'org.geysermc.geyser:api:2.7.0-SNAPSHOT'
    compileOnly "net.kyori:adventure-text-serializer-legacy:4.21.0"
    compileOnly "net.kyori:adventure-text-serializer-plain:4.21.0"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.3.0'
    compileOnly 'com.github.LeonMangler:SuperVanish:6.2.18-3'
    compileOnly 'de.hexaoxi:carbonchat-api:3.0.0-beta.26'
    compileOnly 'com.github.mbax:VanishNoPacket:3.22'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'net.william278.husktowns:husktowns-common:3.0.5'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.9.0-4'
    // Local
    implementation fileTree(dir: 'libs', include: '*.jar')
    compileOnly fileTree(dir: 'libs/compileonly', include: '*.jar')
}

configurations.implementation {
    exclude group: 'joda-time', module: 'joda-time'
    exclude group: 'org.atteo.classindex', module: 'classindex'
}

tasks.named('jar').configure { enabled = true }

tasks.shadowJar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    archiveBaseName.set("$project.name")
    archiveClassifier.set('')
    archiveVersion.set(pluginManifest.version.getFullVersion())

    relocate 'com.github.stefvanschie.inventoryframework', 'com.cyr1en.inventoryframework'
    relocate 'net.wesjd.anvilgui', 'com.cyr1en.anvilgui'
    relocate 'de.rapha149.signgui', 'com.cyr1en.signgui'
    relocate 'dev.jorel.commandapi', 'com.cyr1en.commandapi'
    relocate 'org.fusesource.jansi', 'com.cyr1en.jansi'
    relocate 'org.bstats', 'com.cyr1en.bstats'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'projectName': pluginManifest.name,
                'projectVersion': version,
                'projectAuthor': pluginManifest.author,
                'projectDescription': pluginManifest.description,
                'projectEntry': pluginManifest.entry
        )
        include 'paper-plugin.yml'
    }
}

artifacts {
    archives shadowJar
}

publishing {
    repositories {
        maven {
            name = "Kakuno"
            url = uri("https://repo.cyr1en.com/snapshots")
            credentials {
                username = System.getenv("KAKUNO_USER")
                password = System.getenv("KAKUNO_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

class PluginManifest {
    String name, author, description, entry
    Version version
}

class Version {

    String major, minor, patch, fix, classifier, full

    String getFullVersion() {
        full = "$major.$minor.$patch${(fix as Integer) > 0 ? ".$fix" : ''}"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
