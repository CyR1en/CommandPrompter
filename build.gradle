/*
 * MIT License
 *
 * Copyright (c) 2020 Ethan Bacurio
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'java'
    id 'maven-publish'
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

PluginManifest manifest = [
        name       : 'CommandPrompter',
        version    : new Version(major: 2, minor: 0, patch: 1, fix: 0, classifier: 'SNAPSHOT'),
        author     : 'CyR1en',
        description: 'Perfect companion plugin for inventory UI menu.',
        entry      : 'com.cyr1en.commandprompter.CommandPrompter'
]

group = 'com.cyr1en'
version = manifest.version.toString()

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo.cyr1en.com/snapshots' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    flatDir { dirs 'libs' }
}

dependencies {
    implementation 'me.lucko:commodore:2.2'
    implementation 'com.cyr1en:kiso-utils:1.6-SNAPSHOT'
    implementation 'com.cyr1en:kiso-mc:1.7-SNAPSHOT'
    implementation 'net.wesjd:anvilgui:1.6.3-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.7'
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.0'
    implementation fileTree(dir: 'libs', include: '*.jar')

    compileOnly fileTree(dir: 'libs/compileonly', include: '*.jar')
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly "org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT"
    compileOnly 'com.github.LeonMangler:SuperVanish:6.2.6-4'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    compileOnly 'org.jetbrains:annotations:23.0.0'
}

configurations.implementation {
    exclude group: 'joda-time', module: 'joda-time'
    exclude group: 'org.atteo.classindex', module: 'classindex'
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    archiveBaseName.set("$project.name")
    archiveClassifier.set(manifest.version.classifier)
    archiveVersion.set(manifest.version.getFullVersion())

    relocate 'com.github.stefvanschie.inventoryframework', 'com.cyr1en.inventoryframework'
    relocate 'net.wesjd.anvilgui', 'com.cyr1en.anvilgui'
    relocate 'me.lucko.commodore', 'com.cyr1en.commodore'
    relocate 'org.fusesource.jansi', 'com.cyr1en.jansi'
    relocate 'org.bstats', 'com.cyr1en.bstats'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'projectName': manifest.name,
                'projectVersion': version,
                'projectAuthor': manifest.author,
                'projectDescription': manifest.description,
                'projectEntry': manifest.entry
        )
        include 'plugin.yml'
    }
}

artifacts {
    archives shadowJar
}

publishing {
    repositories {
        maven {
            name = "Kakuno"
            url = uri("https://repo.cyr1en.com/snapshots")
            credentials {
                username = System.getenv("KAKUNO_USER")
                password = System.getenv("KAKUNO_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

class PluginManifest {
    String name, author, description, entry
    Version version
}

class Version {

    String major, minor, patch, fix, classifier, full

    String getFullVersion() {
        full = "$major.$minor.$patch${(fix as Integer) > 0 ? ".$fix" : ''}"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
